import groovy.json.JsonSlurper

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def dartEnv = project.property('dart-defines')
        .split(',')
        .collectEntries { entry ->
            def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
            [(pair.first()): pair.last()]
        }

def getBrand = {
    // Read brands from file.
    def brandsFile = file('../../lib/brands.dart')
    // Remove the first four and last two lines to make it valid JSON.
    def content = brandsFile.readLines().drop(4).dropRight(1).join()

    String brandId
    if (dartEnv == null || !dartEnv.containsKey('BRAND')) {
        brandId = 'vialer'
    } else {
        brandId = dartEnv['BRAND']
    }

    def brands = new JsonSlurper().parseText(content)

    return brands.find { it.identifier == brandId }
}

def brand = getBrand()
def segmentKey = dartEnv['SEGMENT_ANDROID_KEY'] ?: ""

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 30

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.res.srcDirs += "src/${brand.identifier}/res"
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId brand.appId
        resValue "string", "app_name", brand.appName
        resValue "string", "segment_key", segmentKey
        minSdkVersion 26
        targetSdkVersion 30
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def KEYSTORE_FILE_NAME = "keystore.jks"
    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by CodeMagic.
                storeFile file(System.getenv()["FCI_BUILD_DIR"] + "/" + KEYSTORE_FILE_NAME)
                storePassword System.getenv()["FCI_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["FCI_KEY_ALIAS"]
                keyPassword System.getenv()["FCI_KEY_PASSWORD"]
            }
        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            if (System.getenv()["CI"]) { // CI=true is exported by Codemagic
                signingConfig signingConfigs.release
            } else {
                // Signing with the debug keys outside of CI.
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
}

apply plugin: 'com.google.gms.google-services'