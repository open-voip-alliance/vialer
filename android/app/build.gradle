import groovy.json.JsonSlurper

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def brand = [:]

if (project.hasProperty('identifier')) {
    brand.identifier = project.getProperty('identifier')
    brand.appId = project.getProperty('appId')
} else {
    logger.error("""
Brand identifier is missing, this likely means you have not included --dart-define-from-file when
running via Flutter.

We will fallback to the information from vialer.json as this allows us to compile the app in a
purely Android environment. If you are not in a purely Android environment you should resolve this
instead.
""")
    brand.identifier = 'vialer'
    brand.appId = 'com.voipgrid.vialer'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion 34

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.res.srcDirs += "src/${brand.identifier}/res"
    }


    defaultConfig {
        applicationId brand.appId
        minSdkVersion 26
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }

    signingConfigs {
        release {
            if (System.getenv()["CI"]) { // CI=true is exported by Codemagic
                storeFile file(System.getenv()["FCI_KEYSTORE_PATH"])
                storePassword System.getenv()["FCI_KEYSTORE_PASSWORD"]
                keyAlias System.getenv()["FCI_KEY_ALIAS"]
                keyPassword System.getenv()["FCI_KEY_PASSWORD"]
            }

        }
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    namespace 'com.voipgrid.vialer'
    lint {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }
}

task generateNativeValues(type:Exec) {
    workingDir '../..'

    println 'Generating native values..'
    commandLine 'sh', '-c', "dart utils/generate_native_values.dart ${brand.identifier}"
    println 'Done.'
}

preBuild.dependsOn generateNativeValues

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.play:app-update-ktx:2.0.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.firebase:firebase-messaging:21.0.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'com.github.LogentriesCommunity:le_android:4.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.activity:activity-compose:1.8.1'
    implementation 'androidx.compose.material:material:1.5.4'
    implementation 'androidx.compose.animation:animation:1.5.4'
    implementation 'androidx.compose.ui:ui-tooling:1.5.4'
    implementation 'com.segment.analytics.android:analytics:4.10.4'
    implementation 'io.coil-kt:coil-compose:1.4.0'
    implementation 'com.github.vestrel00.contacts-android:core:0.2.4'
    implementation 'com.google.code.gson:gson:2.9.1'
    def lifecycle_version = "2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
}

configurations.all {
    // Segment pulls in this "appsflyer" library as an integration, this is flagged by Google Play
    // as an "ad SDK". This doesn't really seem to be the case but we don't use these integrations
    // so we will just make sure they are excluded.
    exclude group: "com.appsflyer", module: "segment-android-integration"

    // We will also be cautious and remove the Amplitude integrations, because it does a pretty
    // similar thing to AppsFlyer so could be flagged in the future.
    //
    // Note: We use Amplitude internally but all this data is funneled through Segment, we don't
    // send any data to Amplitude directly so don't need any integrations in the app itself.
    exclude group: "com.amplitude", module: "android-sdk"
    exclude group: "com.segment.analytics.android.integrations", module: "amplitude"
}

apply plugin: 'com.google.gms.google-services'